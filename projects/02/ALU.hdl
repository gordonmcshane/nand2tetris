// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:

    Not(in=zx, out=notzx);
    Not(in=zy, out=notzy);
    Mux16(sel=notzx, a=false, b=true, out=notzx16);
    Mux16(sel=notzy, a=false, b=true, out=notzy16);
    And16(a=x, b=notzx16, out=zerox);
    And16(a=y, b=notzy16, out=zeroy);
    Not16(in=zerox, out=negx);
    Not16(in=zeroy, out=negy);
    Mux16(sel=nx, a=zerox, b=negx, out=prefx);
    Mux16(sel=ny, a=zeroy, b=negy, out=prefy);
    Add16(a=prefx, b=prefy, out=addf);
    And16(a=prefx, b=prefy, out=andf);
    Mux16(a=andf, b=addf, sel=f, out=fout);
    Not16(in=fout, out=nfout);
    Mux16(a=fout, b=nfout, sel=no, out=out, out[0..7]=postflsb, out[8..15]=postfmsb, out[15]=ng);
    Or8Way(in=postflsb, out=orlsb);
    Or8Way(in=postfmsb, out=ormsb);
    Or(a=orlsb,b=ormsb, out=nzr);
    Not(in=nzr, out=zr);

}